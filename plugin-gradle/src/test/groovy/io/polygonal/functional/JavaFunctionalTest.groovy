/*
 * This Groovy source file was generated by the Gradle 'init' task.
 */
package io.polygonal.functional

import org.gradle.testkit.runner.GradleRunner
import org.gradle.testkit.runner.UnexpectedBuildFailure
import spock.lang.Specification

class JavaFunctionalTest extends Specification {
    def "can run task"() {
        given:
        def projectDir = new File("build/e2eTest")
        projectDir.mkdirs()
        new File(projectDir, "code/java/org/a/example").mkdirs()
        new File(projectDir, "code/java/org/a/example/domain1/dto").mkdirs()
        new File(projectDir, "code/java/org/a/example/domain2/abc/defg").mkdirs()
        new File(projectDir, "code/java/org/a/example/domain2/abc/defg/Foo.java").write("public class Foo { }")
        new File(projectDir, "code/java/org/a/example/domain2/dto").mkdirs()
        new File(projectDir, "settings.gradle") << ""
        new File(projectDir, "build.gradle") << """
            plugins {
                id('java')
                id('io.polygonal')
            }
            
            polygonalArchitecture {
                sourcesDir = file('code/java')
                basePackage = 'org.a.example'
                
                polygon {
                  packageDef {
                    name = ''
                    publicScope = -1
                  }
                
                  packageDef {
                    name = 'abc'
                  }
                
                  packageDef {
                    name = 'dto'
                  }
                  
                  packageDef {
                    name = 'abc.defg'
                    publicScope = -1
                  }
                }
            }
        """

        when:
        def runner = GradleRunner.create()
        runner.forwardOutput()
        runner.withPluginClasspath()
        runner.withArguments("verifyPolygons")
        runner.withProjectDir(projectDir)
        def result = runner.build()

        then:
        result.output.contains("Task :verifyPolygons")
        !result.output.contains("ConditionException")

        cleanup:
        projectDir.deleteDir()
    }

    def "should not allow invalid polygon"() {
        given:
        def projectDir = new File("build/e2eTest")
        projectDir.mkdirs()
        new File(projectDir, "src/main/java/org/example").mkdirs()
        new File(projectDir, "src/main/java/org/example/domain1/dto").mkdirs()
        new File(projectDir, "src/main/java/org/example/domain1/dto/Foo.java").write("public class Foo { }")
        new File(projectDir, "src/main/java/org/example/domain2/dto").mkdirs()
        new File(projectDir, "src/main/java/org/example/domain2/dto/Foo.java").write("class Foo { }")
        new File(projectDir, "src/main/java/org/example/domain3/dto").mkdirs()
        new File(projectDir, "src/main/java/org/example/domain3/dto/Foo.java").write("interface Foo { }")
        new File(projectDir, "settings.gradle") << ""
        new File(projectDir, "build.gradle") << """
            plugins {
                id('java')
                id('io.polygonal')
            }
            
            polygonalArchitecture {
                sourcesDir = file('src/main/java')
                basePackage = 'org.example'
                
                polygon {
                  packageDef {
                    name = ''
                    publicScope = -1
                  }
                
                  packageDef {
                    name = 'dto'
                    types = ['class']
                    publicScope = 0
                  }
                }
            }
        """

        when:
        def runner = GradleRunner.create()
        runner.forwardOutput()
        runner.withPluginClasspath()
        runner.withArguments("verifyPolygons")
        runner.withProjectDir(projectDir)
        def result = runner.build()

        then:
        thrown(UnexpectedBuildFailure)

        cleanup:
        projectDir.deleteDir()
    }
}
