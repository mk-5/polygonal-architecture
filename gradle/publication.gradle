apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'pl.mk5.polygonal'
version = project.version && project.version != 'unspecified' ? project.version : 'latest'

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

pluginBundle {
    website = 'https://polygonal.io'
    vcsUrl = 'https://github.com/mk-5/polygonal-architecture'
    tags = ['java', 'gradle', 'architecture', 'polygonal architecture', 'groovy', 'clean architecture']
}

gradlePlugin {
    plugins {
        polygonalPlugin {
            id = 'pl.mk5.polygonal-architecture'
            displayName = 'Polygonal Architecture plugin'
            description = 'A plugin that helps you to provide polygonal architecture inside your app!'
            implementationClass = 'pl.mk5.polygonal.PolygonalArchitecturePlugin'
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId = 'polygonal-architecture'

            pom {
                name = "Polygonal Architecture"
                description = "A plugin that helps you to provide polygonal architecture inside your app!"
                url = "https://polygonal.io"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "mk-5"
                        name = "mk-5"
                        email = "mateusz@mk5.pl"
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    override = true
    pkg {
        repo = 'maven'
        name = 'polygonal-architecture'
        desc = 'A plugin that helps you to provide polygonal architecture inside your app!'
        websiteUrl = 'https://polygonal.io'
        vcsUrl = 'https://github.com/mk-5/polygonal-architecture'
        issueTrackerUrl = 'https://github.com/mk-5/polygonal-architecture/issues'
        licenses = ["Apache-2.0"]
        labels = ['java', 'gradle', 'architecture', 'polygonal architecture', 'groovy', 'clean architecture']
        githubRepo = 'https://github.com/mk-5/polygonal-architecture'
        githubReleaseNotesFile = 'README.md'
        publish = true
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = 'A plugin that helps you to provide polygonal architecture inside your app!'
            gpg {
                sign = true
            }
        }
    }
}
